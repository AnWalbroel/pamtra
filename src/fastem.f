       SUBROUTINE FASTEM(TS, U10MPS, freqghz,thet, RVERTR, RHORZR)
C
C**** *FASTEM* -  ROUTINE TO CALCULATE FAST OCEAN EMISSIVITY
C                                 FOR *RT* CALCULATION
C
C PURPOSE.
C --------
C       ROUTINE
C       TO CALCULATE VERTICAL AND HORIZONTAL POLARISED REFLECTIVITIES
C       GIVEN XPERM AT LOCAL INCIDENCE ANGLE FOR ALL CHANNELS
C       AND PROFILES
C
C** INTERFACE.
C   ----------
C       *CALL* *FASTEM
C     TS             (INPUT) R4  OCEAN SKIN TEMPERATURE INCREMENT K
C     U10MPS         (INPUT) R4  10M NEUTRAL WIND SPEED INCREMENT (M/S)
C     EVERT         (OUTPUT) R4  EMISSIVITY INCREMENT IN V-POLARISATION
C     EHORZ         (OUTPUT) R4  EMISSIVITY INCREMENT IN H-POLARISATION
C
C       ALSO TAKES INPUT FROM *COMMON/(VARIABLES)/ *
C       AND *COMMON/CONSTANTS/ *.
C       AND PUTS OUTPUT IN *COMMON/(OUTPUT/ *.
C METHOD.
C -------
C       SEE REFERENCES.
C EXTERNALS.
C ----------
C       USES COMPLEX ARITHMETIC SO FORTRAN COMPLEX FUNCTIONS ARE
C       REQUIRED
C REFERENCE.
C ----------
C       NWP TECH MEMO #?
C AUTHOR.
C -------
C       S.J.ENGLISH        *UKMO*     15/12/97
C MODIFICATIONS.
C --------------
C
C*     *COMMON*
C           SEE *COMMON* DECK.       
c#include "cparam.h"       
c#include "emismw.h"       


      COMPLEX XPERM
      REAL XCORR2(2),EMC(59),THETA,thet
      
      REAL TS,U10MPS,FREQGHZ

      data EMC/.175350E+02,-.617670E+00,.894800E-02,.318420E+01,
     .         .191890E-01,-.108730E-01,.258180E-03,.683960E+02,
     .         -.406430E+00,.228320E-01,-.530610E-03,.476290E+01,
     .         .154100E+00,-.337170E-01,.844280E-03,.782870E+02,
     .         -.434630E-02,.531250E+01,-.114770E-01,.314159E+01,
     .         -.100000E+01,.195000E-04,.255000E+01,-.637182E+01,
     .         .253918E-01,.357569E-04,.942928E+01,-.332839E-01,
     .         -.647724E-04,-.329282E+01,.965450E-02,.281588E-04,
     .         .252676E+00,.343867E-02,-.156362E-04,-.156669E-03,
     .         .139485E-04,-.407633E-07,-.141316E+00,-.356556E-02,
     .         .142869E-04,-.240701E+01,-.563888E-01,.325227E-03,
     .         .296005E+01,.704675E-01,-.426440E-03,-.751252E+00,
     .         -.191934E-01,.125937E-03,-.288253E+00,-.102655E-02,
     .         .226701E-05,-.119072E-02,-.263165E-04,.114597E-06,
     .         .406300E+00,.200031E-02,-.781635E-05/



C      print*,'freq (GHZ) theta (degrees)  windspeed (m/s) ts (K)'
C      read*,FREQGHZ,THETA,U10MPS,TS

c      TS=290.
c      U10MPS=21.00
c      FREQGHZ=20.

      THETA=THET*3.1415927/180.

C CALCULATE PIOM (ELLISON ET AL.) XPERM
      CALL DCLAMKAOUCHI (EMC, TS, FREQGHZ, XPERM)
C CALCULATE COMPLEX FRESNEL REFLECTION COEFFICIENTS
      CALL FRESNEL (EMC, THETA, XPERM, RVERTS, RHORZS)
C CALCULATE SMALL SCALE XCORR TO REFLECTION COEFFICIENTS
      CALL SMALL_SCALE (EMC, THETA, FREQGHZ, U10MPS, XCORR1)
      RV=RVERTS*XCORR1
      RH=RHORZS*XCORR1
C CALCULATE LARGE SCALE GEOMETRIC CORRECTION
      CALL LARGE_SCALE (EMC, THETA, FREQGHZ, U10MPS, XCORR2)
      EVERTR=1.0-RV+XCORR2(1)
      EHORZR=1.0-RH+XCORR2(2)
C CALCULATE FOAM EMISSIVITY CORRECTION
      CALL FOAM_fastem (EMC, U10MPS, FFOAM)

      EVERT=EVERTR - FFOAM*EVERTR+ FFOAM
      EHORZ=EHORZR - FFOAM*EHORZR + FFOAM   
      
      RVERTR = 1-EVERT
      RHORZR = 1-EHORZ
      
C      print*,'Ref vertical',RVERTR
C      print*,'Ref horizontal',RHORZR

      RETURN

      END subroutine
cccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccc

        SUBROUTINE DCLAMKAOUCHI(EMC, TS, FREQGHZ, XPERM)
C
C**** *DCLAMKAOUCHI* -  DIRECT MODEL OF XPERM CALCULATION FOR *RT* CALCULATION
C
C PURPOSE.
C --------
C       ROUTINE
C       TO CALCULATE XPERM OF SALINE WATER BASED ON
C       PIOM MODEL.
C
C** INTERFACE.
C   ----------
C       *CALL* *DCLAMKAOUCHI (TS,FREQGHZ)
C       TS       (INPUT) R4  SEA SKIN TEMPERATURE INCREMENT (K)
C	XPERM (OUTPUT)  C8  XPERM INCREMENT OF SEA WATER
C
C       ALSO TAKES INPUT FROM *COMMON/(VARIABLES)/ *
C       AND *COMMON/CONSTANTS/ *.
C       AND PUTS OUTPUT IN *COMMON/(OUTPUT/ *.
C METHOD.
C -------
C       SEE REFERENCE.
C EXTERNALS.
C ----------
C       USES COMPLEX ARITHMETIC SO FORTRAN FUNCTIONS CMPLX, CSQRT
C       AND CONJG ARE REQUIRED.
C REFERENCES.
C ----------
C       [1] LAMKAOUCHI K., BALANA A. AND ELLISON W.J., 1997:
C       NEW XPERM DATA FOR SEA WATER (30-100 GHZ).
C       DRAFT ESA REPORT.
C       
C       [2] NWP TECH MEMO #?
C AUTHOR.
C -------
C       S.J.ENGLISH        *UKMO*     31/10/97
C MODIFICATIONS.
C --------------
C       NONE
C
C       IMPLIPCCT LOGICAL(L), CHARACTER*8(C)
C
c      USE DISTIO , ONLY : MRFSFILE 
c      USE YOMLUN , ONLY : NULOUT
c#include "yomlun.h"
C
C*     *COMMON*
C           SEE *COMMON* DECK.
c#include "cparam.h"
c#include "emismw.h"
       COMPLEX XPERM
C CONVERT FROM KELVIN TO CENTIGRATE AND DEFINE QUADRATIC AND
C CUBIC FUNCTIONS FOR LATER POLYNOMIALS

        REAL EMC(59)

        NULOUT=6
        TC=TS-273.15
        TC2=TC*TC
        TC3=TC2*TC

C CHECK INPUTS
         IF (TC.LT.-5.0.OR.TC.GT.100.0.OR.FREQGHZ.LT.10.0.OR.
     &        FREQGHZ.GT.500.0) THEN
                   WRITE (NULOUT, *) 'SEVERE WARNING FROM DCLAMKAOUCHI'
                   WRITE (NULOUT, *) FreqGHz,TS
            ELSE
            IF (FREQGHZ.LT.20.0.OR.FREQGHZ.GT.200.0) THEN
                  WRITE (NULOUT,*) 'MODERATE WARNING FROM DCLAMKAOUCHI'
                  WRITE (NULOUT,*) FreqGHz,TS
            ENDIF
        ENDIF
C DEFINE TWO RELAXATION FREQUENPCCES, TAU1 AND TAU2
        TAU1=EMC(1)+EMC(2)*TC+EMC(3)*TC2
        TAU2=EMC(4)+EMC(5)*TC+EMC(6)*TC2+EMC(7)*TC3
C STATIC XPERM ESTATIC=DEL1+DEL2+EINF
        DEL1=EMC(8)+EMC(9)*TC+EMC(10)*TC2+EMC(11)*TC3
        DEL2=EMC(12)+EMC(13)*TC+EMC(14)*TC2+EMC(15)*TC3
        EINF=EMC(18)+EMC(19)*TC
C CALCULATE XPERM USING DOUBLE-DEBYE FORMULA
        FEN=2.0*EMC(20)*FREQGHZ*0.001
        FEN2=FEN**2.0
        DEN1=1.0+FEN2*TAU1*TAU1
        DEN2=1.0+FEN2*TAU2*TAU2
        PERM_REAL1=DEL1/DEN1
        PERM_REAL2=DEL2/DEN2
        PERM_IMAG1=DEL1*FEN*TAU1/DEN1
        PERM_IMAG2=DEL2*FEN*TAU2/DEN2
        PERM_REAL=PERM_REAL1+PERM_REAL2+EINF
        PERM_IMAG=PERM_IMAG1+PERM_IMAG2
        XPERM=CMPLX(PERM_REAL,PERM_IMAG)
C RETURN VALUE OF XPERM TO MAIN PROGRAMME.
        RETURN
        END
cccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccc

      SUBROUTINE FRESNEL(EMC, THETA, XPERM, RVERTS, RHORZS)
C
C**** *FRESNEL* -  FRESNEL CALCULATION FOR *RT* CALCULATION
C
C PURPOSE.
C --------
C       ROUTINE
C       TO CALCULATE VERTICAL AND HORIZONTAL POLARISED REFLECTIVITIES
C       GIVEN XPERM AT LOCAL INPCCDENCENCE ANGLE FOR ALL CHANNELS
C       AND PROFILES
C
C** INTERFACE.
C   ----------
C       *CALL* *FRESNEL
C
C       ALSO TAKES INPUT FROM *COMMON/(VARIABLES)/ *
C       AND *COMMON/CONSTANTS/ *.
C       AND PUTS OUTPUT IN *COMMON/(OUTPUT/ *.
C METHOD.
C -------
C       SEE REFERENCES.
C EXTERNALS.
C ----------
C       USES COMPLEX ARITHMETIC SO FORTRAN COMPLEX FUNCTIONS ARE
C       REQUIRED
C REFERENCE.
C ----------
C       NWP TECH MEMO #?
C AUTHOR.
C -------
C       S.J.ENGLISH        *UKMO*     31/10/97
C MODIFICATIONS.
C --------------
C
C*     *COMMON*
C           SEE *COMMON* DECK.
c#include "cparam.h"
c#include "emismw.h"

       REAL EMC,THETA
       COMPLEX PERM1, PERM2,RVTH,RHTH
       COMPLEX XPERM
        
       PS2=SIN(THETA)*SIN(THETA)
       PCC=COS(THETA)

       PERM1 = CSQRT(XPERM - PS2)
       PERM2  = XPERM*PCC
       RHTH = (   PCC - PERM1)/(   PCC + PERM1)                     
       RVTH = (PERM2 - PERM1)/(PERM2 + PERM1)
       RVERTSR=DBLE(RVTH)
       RVERTSI=AIMAG(RVTH)
       RVERTS=RVERTSR*RVERTSR+RVERTSI*RVERTSI
       RHORZSR=DBLE(RHTH)
       RHORZSI=AIMAG(RHTH)
       RHORZS=RHORZSR*RHORZSR+RHORZSI*RHORZSI

       RETURN
       END

cccccccccccccccccccccccccccccccccccccccccccccccccccccccc

        SUBROUTINE SMALL_SCALE(EMC, THETA, FREQGHZ, U10MPS, XCORR1)
C**** *SMALL_SCALE* -  MODIFIES FRESNEL
C       REFLECTION COEFFICIENTS FOR SMALL SCALE RIPPLES.
C PURPOSE.
C --------
C       TO CALCULATE A CORRECTION TO THE FRESNEL REFLECTION COEFFICIENTS
C       ALLOWING FOR THE PRESENCE OF SMALL RIPPLES.
C
C** INTERFACE.
C   ----------
C       *CALL* *SMALL_SCALE_CORRECTION.F
C       U10MPS       (INPUT) R4  10M NEUTRAL WIND SPEED (M/S)
C       XCORR1     (OUTPUT)  R4  REFLECTIVITY CORRECTION   
C
C       ALSO TAKES INPUT FROM *COMMON/(VARIABLES)/ *
C       AND *COMMON/CONSTANTS/ *.
C       AND PUTS OUTPUT IN *COMMON/(OUTPUT/ *.
C METHOD.
C -------
C       SEE REFERENCES.
C EXTERNALS.
C ----------
C       NONE
C REFERENCE.
C ----------
C       [1] ENGLISH S.J., 1997: A FAST EMISSIVITY MODEL FOR
C              ATOVS. NWP TECHNICAL REPORT (BEING WRITTEN).
C
C       [2] WU, S. T., AND FUNG A.K., 1972:  A NONCOHERENT MODEL
C	       FOR MICROWAVE EMISSION AND BACKSCATTERING FROM THE SEA
C              SURFACE. J.G.R., 77, 5917-5929.
C
C       [3] GUILLOU C., 1996: ETUDE DU TRANSFERT RADIATIF DANS
C              L'ATMOSPHERE EN MICRO-ONDES, A PARTIR D'OBSERVATIONS
C              RADIOMETRIQUES AEROPORTEES. THESE, UNIVERSITE DE PARIS.
C AUTHOR.
C -------
C       S.J.ENGLISH        *UKMO*     6/11/97
C MODIFICATIONS.
C --------------
C
C*     *COMMON*C           SEE *COMMON* DECK.
c#include "cparam.h"
c#include "emismw.h"
C        
      REAL EMC(59),THETA,FREQGHZ
      
      PC2=COS(THETA)*COS(THETA)

      IF (FREQGHZ.GT.0.1) THEN
         XCORR1=EXP(EMC(21)*U10MPS*PC2/(FREQGHZ*FREQGHZ))
      ELSE
         XCORR1=1.0
      ENDIF
      RETURN
      END
ccccccccccccccccccccccccccccccccccccccccccccccccccccccccccc

        SUBROUTINE LARGE_SCALE(EMC, THETA, FREQGHZ, U10MPS, XCORR2)
C**** *LARGE_SCALE_CORRECTION* -  MODIFIES FRESNEL
C       REFLECTION COEFFICIENTS FOR GEOMETRIC REFLECTIONS
C PURPOSE.
C --------
C       ROUTINE: TO CALCULATE A CORRECTION TO THE FRESNEL REFLECTION COEFFICIENTS
C       ALLOWING FOR THE PRESENCE OF LARGE SCALE ROUGHNESS      
C
C** INTERFACE.
C   ----------
C       *CALL* *LARGE_SCALE_CORRECTION.F
C       U10MPS       (INPUT) R4  10M NEUTRAL WIND SPEED (M/S)
C       XCORR2     (OUTPUT)  R4  REFLECTIVITY CORRECTION   
C
C       ALSO TAKES INPUT FROM *COMMON/(VARIABLES)/ *
C       AND *COMMON/CONSTANTS/ *.
C       AND PUTS OUTPUT IN *COMMON/(OUTPUT/ *.
C METHOD.
C -------
C       SEE REFERENCES.
C EXTERNALS.
C ----------
C         NONE
C REFERENCE.
C ----------
C        [1] ENGLISH S.J., 1997: A FAST EMISSIVITY MODEL FOR
C              ATOVS. NWP TECHNICAL REPORT (BEING WRITTEN).
C AUTHOR.
C -------
C       S.J.ENGLISH        *UKMO*     3/11/97
C MODIFICATIONS.
C --------------
C
C           SEE *COMMON* DECK.
c#include "cparam.h"
c#include "emismw.h"
      REAL ZC(6), XCORR2(2), EMC (59),THETA, FREQGHZ


C EFFECTIVE SPECULAR EMISSIVITY CALCULATED
C
C CALCULATE FREQUENCY, WINDSPEED AND VIEW ANGLE SQUARED FOR
C POLYNOMIALS
      FREQGHZ2=FREQGHZ*FREQGHZ
      U10MPS2=U10MPS*U10MPS

      PCC=COS(THETA)

      SEC=1.0/PCC
      SEC2=SEC*SEC
      USEC=U10MPS*SEC
C JP=1 => V POL, JP=2 => H POL 
C JP: TWO POLARISTIONS
      DO 250 JP=1,2
C JC: SIX COEFFICIENTS (CONSTANT, U, U^2, SEC, SEC^2, U*SEC)	
         DO 260 JC=1,6
C SELECT ELEMENT FROM DATA ARRAY EMC ELEMENTS 24-59 FOR THIS MODEL
            IEMC=24+(JC-1)*3+(JP-1)*18
C COEFFICIENTS 1-5 FOR THIS POLARISATION STORED IN ZC
            ZC(JC)=EMC(IEMC)+EMC(IEMC+1)*FREQGHZ
     &                +EMC(IEMC+2)*FREQGHZ2
 260     CONTINUE
C CALCULATE CORRECTION FOR THIS POLARISATION
         XCORR2(JP)=ZC(1)+ZC(2)*SEC+ZC(3)*SEC2
     &                            +ZC(4)*U10MPS+ZC(5)*U10MPS2+ZC(6)*USEC
         XCORR2(JP)=XCORR2(JP)/100.0
 250     CONTINUE
      RETURN
      END
ccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccc

        SUBROUTINE FOAM_fastem (EMC, U10MPS, FFOAM)
C**** *FOAM* -  MODEL OF FOAM COVERAGE FOR *RT* CALCULATION
C
C PURPOSE.
C --------
C       ROUTINE
C       TO CALCULATE FOAM COVERAGE AT A GIVEN NEUTRAL
C       STABILITY WINDSPEED. IN PRACTISE THE 10M WINDSPEED
C       IS USUALLY USED WITHOUT A STABILITY CORRECTION.
C
C** INTERFACE.
C   ----------
C       *CALL* *FOAM_CORRECTION
C       U10MPS       (INPUT)   R4  10M NEUTRAL WIND SPEED (M/S)
C       FFOAM        (OUTPUT)  R4  FOAM COVERAGE FRACTIONC
C       ALSO TAKES INPUT FROM *COMMON/(VARIABLES)/ *
C       AND *COMMON/CONSTANTS/ *.
C       AND PUTS OUTPUT IN *COMMON/(OUTPUT/ *.
C METHOD.
C -------
C       SEE REFERENCES.
C EXTERNALS.
C ----------
C       NONE.
C REFERENCE.
C ----------
C       NWP TECH MEMO #?
C
C       [1] MONAHAN E.C. AND O'MUIRCHEARTAIGH I.G., 1986: WHITECAPS
C             AND THE PASSIVE REMOTE SENSING OF THE OCEAN'S SURFACE., 
C             INT. J. OF REMOTE SENSING, 7 (NO 5), 627-642.
C
C       [2] WU S.T., 1979: OCEANIC WHITECAPS AND SEA STATE., J. PHYS.
C              OCEANOGRAPHY, 9, 1064-1078.
C
C AUTHOR.
C -------
C       S.J.ENGLISH        *UKMO*     6/11/97
C MODIFICATIONS.
C --------------
C
C 
C*     *COMMON*C           SEE *COMMON* DECK.
c#include "cparam.h"
c#include "emismw.h"
C        
       REAL EMC(59)

       FFOAM=EMC(22)*(U10MPS**EMC(23))
C
       RETURN

       END

ccccccccccccccccccccccccccccccccccccccccccccccccccccccccccc
